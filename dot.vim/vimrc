" https://rbtnn.hateblo.jp/entry/2014/11/30/174749
set encoding=utf-8
scriptencoding utf-8

filetype off
filetype plugin indent off
syntax off


" --------------------------------------
" Variable
"
let g:markdown_recommended_style = 0
let g:netrw_banner = 0
let g:netrw_dirhistmax = 1
let g:netrw_hide = 0
let g:netrw_home = expand('~/.vim')
let g:netrw_liststyle = 0
let g:vim_indent_cont = 0

let g:auto_reload = timer_start(
  \   1000,
  \   {-> execute('silent! checktime')},
  \   {'repeat': -1}
  \ )
let g:my_local_vimrc_path = expand('~/.vim/local_sample.vim')
call my_vimrc#set_preload_vimrc(g:my_local_vimrc_path)


" --------------------------------------
" Option
"
" System
if has('unix')
  language messages C
else
  language messages en
endif
if has('unix') && !has('win32unix')
  set clipboard&
  set clipboard^=unnamedplus
endif
set autoread
set belloff=all
set nobackup
set noswapfile
set noundofile
set nowritebackup

" Edit
" set binary noeol
" set isfname-=|
if has('patch-8.2.0860')
  set nrformats=unsigned
else
  set nrformats=
endif
set backspace=indent,eol,start
set fileencodings=utf-8,euc-jp,cp932
set fileformat=unix
set fileformats=unix,dos,mac
set hidden
set virtualedit=block

" Search
if executable('rg')
  let &grepprg = 'rg --vimgrep --no-heading -uu'
  set grepformat=%f:%l:%c:%m
endif
set hlsearch
set ignorecase
set incsearch
set shortmess+=cS
set smartcase
set wrapscan

" View
set number
set relativenumber
set ambiwidth=double

" Window
set noequalalways
set pumheight=10
set splitbelow
set splitright
if has('patch-8.2.4325')
  set wildmenu wildoptions=pum,tagfile wildchar=<Tab>
else
  set wildmenu wildoptions=tagfile wildchar=<Tab>
endif

" CommandLine
set cmdheight=1
set noshowcmd

" StatusLine
" set fillchars+=stl:\ ,stlnc:\_
set laststatus=2
set noshowmode

" TabLine
set showtabline=2

" Highlight
set colorcolumn=80
set cursorline
set showmatch matchtime=1 matchpairs+=\<:\>
" space:\\u2423,extends:\\u00BB,precedes:\\u00AB
set list listchars=space:␣,tab:>-,trail:~,nbsp:%,extends:»,precedes:«
if &diff
  set nospell
else
  set spelllang+=cjk spell
endif

" Abbreviation
" :w' --> :w
cnoreabbrev w' w


" --------------------------------------
" Keymap
"
nnoremap <silent> j gj
nnoremap <silent> k gk
vnoremap <silent> j gj
vnoremap <silent> k gk

" Insert Mode
" i_CTRL-T Insert one indent
" i_CTRL-D Delete one indent
inoremap ,date <C-r>=strftime('%Y-%m-%d')<CR>

" Location List
nnoremap <silent> <C-n> :<C-u>lnext<CR>
nnoremap <silent> <C-p> :<C-u>lprevious<CR>

" Resize Window
nnoremap <silent> <S-Down>  <C-w>1-
nnoremap <silent> <S-Up>    <C-w>1+
nnoremap <silent> <S-Right> <C-w>10>
nnoremap <silent> <S-Left>  <C-w>10<

" https://lambdalisue.hatenablog.com/entry/2015/12/25/000046
" Edit
nnoremap <Plug>(my-Edit) <Nop>
nmap <Space>e <Plug>(my-Edit)
nnoremap <silent> <Plug>(my-Edit)r :<C-u>%s/\r$//e<CR>
nnoremap <silent> <Plug>(my-Edit)s :<C-u>%s/\s\+$//e<CR>

" Filer
nnoremap <Plug>(my-Filer) <Nop>
nmap <Space>f <Plug>(my-Filer)
nnoremap <silent> <Plug>(my-Filer)b :<C-u>execute 'edit' g:my_bookmark_path<CR>
nnoremap <silent> <Plug>(my-Filer)c :<C-u>15Lexplore<CR>
nnoremap <silent> <Plug>(my-Filer)g :<C-u>execute 'edit' g:my_gtd_path<CR>
nnoremap <silent> <Plug>(my-Filer)l :<C-u>execute 'edit' g:my_local_vimrc_path<CR>
nnoremap <silent> <Plug>(my-Filer)o :<C-u>execute '15Lexplore' expand('%:p:h')<CR>
nnoremap <silent> <Plug>(my-Filer)s :<C-u>execute 'edit' g:my_skk_path<CR>
nnoremap <silent> <Plug>(my-Filer)v :<C-u>execute 'edit' $MYVIMRC<CR>

" eNcoding
nnoremap <Plug>(my-eNcoding) <Nop>
nmap <Space>n <Plug>(my-eNcoding)
nnoremap <silent> <Plug>(my-eNcoding)ec :<C-u>setlocal fileencoding=cp932<CR>
nnoremap <silent> <Plug>(my-eNcoding)ee :<C-u>setlocal fileencoding=euc-jp<CR>
nnoremap <silent> <Plug>(my-eNcoding)eu :<C-u>setlocal fileencoding=utf-8<CR>
nnoremap <silent> <Plug>(my-eNcoding)fd :<C-u>edit ++fileformat=dos<CR>
nnoremap <silent> <Plug>(my-eNcoding)fu :<C-u>edit ++fileformat=unix<CR>
nnoremap <silent> <Plug>(my-eNcoding)nc :<C-u>edit ++encoding=cp932<CR>
nnoremap <silent> <Plug>(my-eNcoding)ne :<C-u>edit ++encoding=euc-jp<CR>
nnoremap <silent> <Plug>(my-eNcoding)nu :<C-u>edit ++encoding=utf-8<CR>

" Switch
nnoremap <Plug>(my-Switch) <Nop>
nmap <Space>s <Plug>(my-Switch)
nnoremap <silent> <Plug>(my-Switch)b :<C-u>setlocal scrollbind! scrollbind?<CR>
nnoremap <silent> <Plug>(my-Switch)n :<C-u>setlocal number! number?<CR>
nnoremap <silent> <Plug>(my-Switch)p :<C-u>setlocal paste! paste?<CR>
nnoremap <silent> <Plug>(my-Switch)r :<C-u>setlocal relativenumber! relativenumber?<CR>
nnoremap <silent> <Plug>(my-Switch)s :<C-u>setlocal spell! spell?<CR>
nnoremap <silent> <Plug>(my-Switch)t :<C-u>setlocal expandtab! expandtab?<CR>
nnoremap <silent> <Plug>(my-Switch)w :<C-u>setlocal wrap! wrap?<CR>


" --------------------------------------
" Command
"
command! -nargs=? C call s:copy2clip(<q-args>)
function s:copy2clip(...) abort
  if a:0 >= 1
    " let @+ = getreg(a:1)
    call setreg('+', getreg(a:1))
  else
    " let @+ = @"
    call setreg('+', @")
  endif
endfunction

command! -nargs=? CW call s:copy2clip_wsl(<q-args>)
function s:copy2clip_wsl(...) abort
  if a:0 >= 1
    if has('win32unix')
      call system('clip.exe', getreg(a:1))
    else
      call system('clip.exe', system('nkf -sc', getreg(a:1)))
    endif
  else
    if has('win32unix')
      call system('clip.exe', @")
    else
      call system('clip.exe', system('nkf -sc', @"))
    endif
  endif
endfunction


" --------------------------------------
" Aucocommand
"
function! s:set_register() abort
  if empty(&buftype)
    call setreg('a', '-------------------')
    call setreg('b', expand('%:p:~'))
    call setreg('c', expand('%:p:~:h'))
    call setreg('d', expand('%:t'))
    call setreg('e', expand('%:t:r'))

    call setreg('x', '-------------------')
    let l:dir_status = 'CWD:' .. fnamemodify(getcwd(), ':~')
    let l:dir_status ..= '  ' .. 'Cfg:' .. fnamemodify(g:my_local_vimrc_path, ":h:t")
    let l:dir_status ..= '  ' .. (has('nvim') ? 'N' : 'V')
    call setreg('y', l:dir_status)
    let l:file_status = (&expandtab ? 'Spaces:' : 'TabSize:') .. &tabstop
    let l:file_status ..= '  ' .. ((&fileencoding != '') ? &fileencoding : &encoding)
    let l:file_status ..= '  ' .. ((&fileformat == 'doc') ? 'CRLF' : 'LF')
    let l:file_status ..= '  ' .. ((&filetype == '') ? 'no_ft' : &filetype)
    call setreg('z', l:file_status)
  endif
endfunction

function! s:my_filetype() abort
  if empty(&filetype)
    return
  endif

  setlocal nofoldenable autoindent expandtab
  setlocal shiftwidth=4 softtabstop=4 tabstop=4

  if index([
    \   'css',
    \   'mermaid',
    \   'plantuml',
    \   'sh',
    \   'toml',
    \   'typescript',
    \   'vim',
    \   'yaml',
    \ ], &filetype) >= 0
    setlocal shiftwidth=2 softtabstop=2 tabstop=2
  elseif index([
    \   'go',
    \   'make',
    \ ], &filetype) >= 0
    setlocal noexpandtab
  else
    " Do nothing.
  endif

  " Linter
  if &filetype == 'sh' && executable('shellcheck')
    let &l:makeprg = 'shellcheck -f gcc -x %'
    let &l:errorformat = '%f:%l:%c: %trror: %m,%f:%l:%c: %tarning: %m,%f:%l:%c: %tote: %m,'
  elseif &filetype == 'dockerfile' && executable('hadolint')
    let &l:makeprg = 'hadolint --no-color %'
    let &l:errorformat = '%f:%l %m'
  else
    " Do nothing.
  endif
endfunction

function! s:my_asyncjob_on_save() abort
  if &filetype == 'python'
    let l:ipynb_path = expand('%:p:r') . '.ipynb'
    if executable('jupytext') && filereadable(l:ipynb_path)
      call my_async#jobstart('jupytext --sync ' . l:ipynb_path)
    endif
  elseif &filetype == 'markdown'
    if exists(':NumberHeader') == 2
      execute 'NumberHeader'
    endif
  elseif &filetype == 'rmd'
    if exists(':NumberHeader') == 2
      execute 'NumberHeader'
    endif
  else
    " Do nothing.
  endif
endfunction

augroup VimrcAutocmd
  autocmd!
  " Register
  autocmd BufEnter * call s:set_register()

  " Auto Reload
  autocmd WinEnter * checktime

  " FileType
  autocmd FileType,BufEnter * call s:my_filetype()

  " Async Job
  autocmd BufWritePost * call s:my_asyncjob_on_save()
augroup END


" --------------------------------------
" dein.vim
"
let s:dein_path = expand('<sfile>:p:h') .. '/dein/dein.vim'
if filereadable(s:dein_path)
  execute 'source' s:dein_path
endif


" --------------------------------------
" dpp.vim
"
let s:dpp_path = expand('<sfile>:p:h') .. '/dpp/dpp.vim'
if filereadable(s:dpp_path)
  " execute 'source' s:dpp_path
endif


" --------------------------------------
" End of setting
"
set background=dark
filetype plugin indent on
syntax enable
if has('termguicolors')
  set termguicolors
endif

if has('patch-9.0.1778')
  execute 'colorscheme' get(g:, 'colors_name', 'retrobox')
endif
